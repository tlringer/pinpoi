apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    defaultConfig {
        applicationId "io.github.fvasco.pinpoi"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 10
        versionName "0.3.3"
        testApplicationId 'io.github.fvasco.pinpoi_test'
    }
    adbOptions {
        timeOutInMs 60000 // set timeout to 1 minute
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

repositories {
    jcenter()
    mavenCentral()

    flatDir {
        dirs 'libs'
    }
}

configurations {
    checkerFrameworkJavac {
        description = 'a customization of the Open JDK javac compiler with additional support for type annotations'
    }

    checkerFrameworkAnnotatedJDK {
       description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
    }
    checkerFramework {
       description = 'The Checker Framework: custom pluggable types for Java'
    }
}

// By default, don't run SPARTA. To run SPARTA, run ../gradlew compileDebugJavaWithJavac -PrunSparta=true
def runSparta = project.properties['runSparta'] ?: false

// To infer annotations, run ../gradlew compileDebugJavaWithJavac -Pinfer=true
def infer = project.properties['infer'] ?: false

dependencies {
    // Support both options and env vars
    def spartaDir = System.env.SPARTA_CODE == null ?
            System.getProperty("SPARTA_CODE") : "$System.env.SPARTA_CODE"

    if (runSparta || infer) {
        // checkerframework
        ext.checkerFrameworkVersion = '1.9.12'
        ext.jdkVersion = 'jdk7'
        checkerFrameworkAnnotatedJDK "org.checkerframework:${jdkVersion}:${checkerFrameworkVersion}"
        checkerFrameworkJavac "org.checkerframework:compiler:${checkerFrameworkVersion}"
        checkerFramework "org.checkerframework:checker:${checkerFrameworkVersion}"
        compile "org.checkerframework:checker:${checkerFrameworkVersion}"
        compile "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    }

    compile fileTree(dir: spartaDir, include: ['*.jar'])

    if (infer) {
        def inferenceDir = System.env.CHECKER_INFERENCE == null ? 
            System.getProperty("CHECKER_INFERENCE") + "/dist" : "$System.env.CHECKER_INFERENCE/dist"

        compile fileTree(dir: inferenceDir, include: ['*.jar'])
    }

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile(name: 'ACG Library-release', ext:'aar')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:support-v4:23.2.0'
    compile 'com.android.support:recyclerview-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
}

if (runSparta) {
    allprojects {
        // SPARTA integration
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile).all {
                options.debug = true
                options.compilerArgs = [
                        '-processor', 'sparta.checkers.intents.IntentChecker',
                        '-AflowPolicy=flow/flow-policy',
                        '-AcomponentMap=sparta-out/component-map',
                        '-AprintErrorStack'
                ]
                options.bootClasspath = System.getProperty("sun.boot.class.path") + ":" + options.bootClasspath
                options.fork = true
                options.forkOptions.jvmArgs += ["-Xbootclasspath/p:${configurations.checkerFrameworkJavac.asPath}"]
            }
        }
    }
}

if (infer) {
    allprojects {
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile).all {
                options.debug = true
                options.compilerArgs = [
                        '-processor', 'checkers.inference.InferenceChecker',
                        '-Xmaxwarns', '1000', 
                        '-Xmaxerrs', '1000', 
                        '-XDignore.symbol.file', 
                        '-AprintErrorStack', 
                        '-Awarns'   
                ]
                options.bootClasspath = System.getProperty("sun.boot.class.path") + ":" + options.bootClasspath
                options.fork = true
                options.forkOptions.jvmArgs += ["-Xbootclasspath/p:${configurations.checkerFrameworkJavac.asPath}"]
            }
        }
    }
}

